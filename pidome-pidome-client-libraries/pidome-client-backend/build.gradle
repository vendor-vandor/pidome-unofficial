// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
apply plugin: 'me.tatarka.retrolambda'
buildscript {
  repositories {
     mavenCentral()
  }

  dependencies {
     classpath 'me.tatarka:gradle-retrolambda:2.5.0'
  }
}
if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.pidome.client.system.demo.ConnectionDemo'
}

configurations {
    publishJar
}

jar {
    from {
        configurations.compile.findAll {  
            it.getName().startsWith("pcl-")
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

dependencies {
    compile project(":pcl-connection-provider")
    compile project(":pcl-backend-interfaces")
    compile project(":pcl-storage-provider")
    compile project(":pcl-data-provider")
    compile project(":pcl-utilities")
    compile project(":pcl-entities")
    runtime "com.googlecode.json-simple:json-simple:1.1.1"
    runtime "org.pidome.thirdparties.dependencies:java-websocket:1.3.1-SNAPSHOT"
    publishJar "org.pidome.client:pidome-client-backend:1.0-SNAPSHOT"
}

uploadArchives {
    File pubPropFile = new File("../private/maven-deploy-props.properties")
    if(pubPropFile.exists()) {
        Properties deployProps = new Properties()
        deployProps.load(new FileInputStream(pubPropFile))
        repositories.mavenDeployer {
            pom.whenConfigured { pom -> pom.dependencies = pom.dependencies.findAll { 
                dep -> !dep.artifactId.startsWith('pcl-') } 
            } 
            configuration = configurations.publishJar
            repository(url: "http://builder.pidome.org:9000/repository/pidome-client-libraries/") {
                authentication(userName: deployProps['archuser'], password: deployProps['archpass'])
            }
        }
    }
}